go athena
# 
line x loc=0.00 spac=0.10
line x loc=0.20 spac=0.006
line x loc=0.40 spac=0.006
line x loc=0.60 spac=0.01
#
line y loc=0.00 spac=0.02
line y loc=0.20 spac=0.005
line y loc=0.50 spac=0.05
line y loc=0.80 spac=0.15
# 
init silicon c.phosphor=1.0e14 orientation=100 space.mult=2.0

#pwell formation including masking off the nwell
# 
diffus time=30 temp=1000 dryo2 press=1.00 hcl.pc=3
# 
etch oxide dry thick=0.02
#
#Pwell implant
# 
implant boron dose=8.0e12 energy=100 tilt=0 rotation=0 crystal
# 
diffus time=100 temp=950 weto2 hcl.pc=3
# 
#welldrive starts here
#
diffus time=50 temp=1000 t.final=1200 dryo2 press=0.100 hcl.pc=3
# 
diffus time=220 temp=1200 nitro press=1.00
# 
diffus time=90 temp=1200 t.final=800.4 nitro press=1.00
# 
etch oxide all
#
#cleaning oxide
# 
diffus time=20 temp=1000 dryo2 press=1.00 hcl.pc=3
# 
etch oxide all
#
#gate oxide grown here
# 
diffus time=11 temp=925 dryo2 press=1.00 hcl.pc=3
#
# Extract a design parameter
#
extract name="gateox" thickness material="SiO~2" mat.occno=1 x.val=0.05
#
#implanting boron
# 
implant boron dose=9.5e11 energy=10 tilt=0 rotation=0 crystal
# 
deposit polysilicon thick=0.2 divisions=10
#
#we operate in 2D
# 
etch polysilicon left p1.x=0.35
# 
method fermi compress
diffus time=11 temp=900 weto2 press=1.00
# 
implant phosphor dose=3.0e13 energy=100 tilt=0 rotation=0 crystal
# 
deposit oxide thick=0.12 divisions=8
# 
etch oxide dry thick=0.12
# 
implant arsenic dose=5.0e15 energy=100 tilt=0 rotation=0 crystal
# 
method fermi compress
diffus time=1 temp=900 nitro press=1.00
#
#contact metal
# 
etch oxide left p1.x=0.2
# 
deposit aluminum thick=0.03 divisions=2
# 
etch aluminum right p1.x=0.18
#
#Extract design parameters
#
#junciton depth
extract name="nxj" xj material="Silicon" mat.occno=1 x.val=0.1 junc.occno=1
#
#n++ region sheet resistance
extract name="n++ sheet rho" sheet.res material="Silicon" mat.occno=1 \
        x.val=0.05 region.occno=1
#
#extract sheet resistance under the spacer, of the LDD region
#
extract name="ldd sheet rho" sheet.res material="Silicon" mat.occno=1 \
        x.val=0.3 region.occno=1
#
#surface conc under the channel
#
extract name="chan surface conc" surf.conc impurity="Net Doping" \
        material="Silicon" mat.occno=1 x.val=0.45
#
#extract curve of conductance vs bias
extract start material="Polysilicon" mat.occno=1 region.occno=1 bias=0.0 \
        bias.step=0.2 bias.stop=2 x.val=0.45
extract done name="sheet cond v bias" curve(bias,1dconduct \
        material="Silicon" mat.occno=1 x.val=0.45 region.occno=1 \
        semi.poly) outfile="extract.dat"
#
#extract the long channel Vt
#
extract name="n1dvt" 1dvt ntype vb = 0.0 qss=1e10 x.val=0.49


#
struct mirror right

#
electrode name=gate x=0.5 y=0.1
#
electrode name=source x=0.1
#
electrode name=drain x=1.1
#
electrode name=substrate backside

structure outfile=simulate_nmos1.str

#plot the structure
tonyplot  simulate_nmos1.str -set mos1ex01_0.set

go atlas

# set material models
models cvt srh print 

contact name=gate n.poly
interface qf=3e10

method newton
solve init

# Bias the drain 
solve vdrain=0.1 

# Ramp the gate
log outf=simulate_nmos1.log master
solve vgate=0 vstep=0.25 vfinal=3.0 name=gate
save outf=simulate_nmos1.str

# plot results
tonyplot  simulate_nmos1.log -set simulate_nmos1_log.set

# extract device parameters
extract name="nvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
	- abs(ave(v."drain"))/2.0)
extract name="nbeta" slope(maxslope(curve(abs(v."gate"),abs(i."drain")))) \
	* (1.0/abs(ave(v."drain")))
extract name="ntheta" ((max(abs(v."drain")) * $"nbeta")/max(abs(i."drain"))) \
	- (1.0 / (max(abs(v."gate")) - ($"nvt")))

quit
